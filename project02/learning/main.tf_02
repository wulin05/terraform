provider "aws" {
  
}

variable "vpc_cidr_block" {
  type = string
}

variable "subnet_cidr_block" {
  type = string
}

variable "avail_zone" {
  type = string
}

variable "env_prefix" {
  type = string
}

variable "acc_security_cidr" {
  type = list(object({
    name = string
    cidr_block = string
  }))
}

resource "aws_vpc" "myapp-vpc" {
  cidr_block = var.vpc_cidr_block
  tags = {
    Name = "${var.env_prefix}-vpc"
  }
}

resource "aws_subnet" "myapp-subnet-1" {
  vpc_id = aws_vpc.myapp-vpc.id
  cidr_block = var.subnet_cidr_block
  availability_zone = var.avail_zone
  tags = {
    Name = "${var.env_prefix}-subnet-1"
  }
}

resource "aws_internet_gateway" "myapp_igw" {
  vpc_id = aws_vpc.myapp-vpc.id
  tags = {
    Name = "${var.env_prefix}-igw"
  }
}

// so we use default route table, no need to creating new one
resource "aws_default_route_table" "main_rtb" {
  // 可以通过: terraform state show aws_vpc.myapp-vpc 来查到上面创建的vpc同时会自动创建的默认route table id的字段名：default_route_table_id
  default_route_table_id = aws_vpc.myapp-vpc.default_route_table_id
  
  // 注意没有用 route = {}
  route {  
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.myapp_igw.id
  }
  tags = {
    Name = "${var.env_prefix}-main-rtb"
  }
}

// Locate the existing default security group in this VPC, then let Terraform take over it and modify the rules.
// but in real production environment we suggest create new security group to modify the rules.
resource "aws_default_security_group" "default_myapp_sg" {
  vpc_id = aws_vpc.myapp-vpc.id

  ingress {
    from_port = 22
    to_port = 22
    protocol = "tcp"
    cidr_blocks = [var.acc_security_cidr[0].cidr_block]
    description = "Private_SSH"
  }

  // allow any others can access this nginx server port
  ingress {
    from_port = 8080
    to_port = 8080
    protocol = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["0.0.0.0/0"] 
    prefix_list_ids = []  
  }

  tags = {
    Name = "${var.env_prefix}-default-sg"
  }
}
